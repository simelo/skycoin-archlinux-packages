# Maintainer: Moses Narrow <moe_narrow@use.startmail.com>
pkgname=cx-dev
_pkgname=cx-dev
pkgver=20190120.867_59f697f
pkgrel=1
pkgdesc="CX Native Blockchain Programming Language"
arch=('i686' 'x86_64')
license=('GPL')
depends=(
    'libxi'
    'libxrandr'
    'libxcursor'
    'mesa'
    'libxinerama'
)
makedepends=(
	'go'
	'git'
)

source=(
	"$_pkgname::git://github.com/skycoin/cx.git#branch=${BRANCH:-develop}"
)

md5sums=(
	'SKIP'
)

backup=(
)

pkgver() {
	if [[ "$PKGVER" ]]; then
		echo "$PKGVER"
		return
	fi

	cd "$srcdir/$_pkgname"
	local date=$(git log -1 --format="%cd" --date=short | sed s/-//g)
	local count=$(git rev-list --count HEAD)
	local commit=$(git rev-parse --short HEAD)
	echo "$date.${count}_$commit"
}

build() {
	cd "$srcdir/$_pkgname"

	if [ -L "$srcdir/$_pkgname" ]; then
		rm "$srcdir/$_pkgname" -rf
		mv "$srcdir/go/src/$_pkgname/" "$srcdir/$_pkgname"
	fi

	rm -rf "$srcdir/go/src"

	mkdir -p "$srcdir/go/src"

	export GOPATH="$srcdir/go"

	mv "$srcdir/$_pkgname" "$srcdir/go/src/"

	cd "$srcdir/go/src/$_pkgname/"
	ln -sf "$srcdir/go/src/$_pkgname/" "$srcdir/$_pkgname"

	echo ":: Updating git submodules"
	git submodule update --init

	echo ":: Building binaries"
	cd "$srcdir/go/src/$_pkgname/"
	./cx.sh
		
}

package() {
	find "$srcdir/go/bin/" -type f -executable | while read filename; do
		install -DT "$filename" "$pkgdir/usr/bin/$(basename $filename)"
	done
}
